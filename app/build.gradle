apply plugin: 'com.android.application'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git tag'.execute([], project.rootDir).text.trim().split("\n").last()
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

def keystoreFilepath = ''
def keystorePSW = ''
def keystoreAlias = ''
def keystoreAliasPSW = ''
// default keystore file, PLZ config file path in local.properties
def keyfile = file('s.keystore.temp')

Properties properties = new Properties()
// local.properties file in the root director
properties.load(project.rootProject.file('local.properties').newDataInputStream())
keystoreFilepath = properties.getProperty("keystore.path")

if (keystoreFilepath) {
    keystorePSW = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPSW = properties.getProperty("keystore.alias_password")
    keyfile = file(keystoreFilepath)
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    signingConfigs {
        myConfig {
            storeFile keyfile
            storePassword keystorePSW
            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
            println("====== signingConfigs.release ======")
        }
    }
    defaultConfig {
        applicationId "gedoor.kunfei.lunarreminder"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 10000 + gitCommits
        versionName gitTag
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    buildTypes {
        debug {
            println("====== buildTypes.debug ======")
            signingConfig signingConfigs.myConfig
        }
        release {
            signingConfig signingConfigs.myConfig
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                output.outputFile = file("${output.outputFile.parent}/Lunar-reminder" +
                        "-${variant.buildType.name.toLowerCase()}" +
                        "-${variant.versionName}" +
                        "-${variant.flavorName.toLowerCase()}" +
                        ".apk")
            }
        }
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
        exclude group: "org.apache.httpcomponents", module: "httpclient"
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:design:25.1.1'
    compile 'com.android.support:support-v4:25.1.1'
    testCompile 'junit:junit:4.12'
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    compile 'pub.devrel:easypermissions:0.3.0'
    compile 'cn.carbs.android:IndicatorView:1.0.2'
    compile 'cn.carbswang.android:NumberPickerView:1.1.0'
    compile 'com.google.apis:google-api-services-calendar:v3-rev239-1.22.0'
    compile 'com.google.android.gms:play-services-auth:10.2.1'
    compile 'com.google.api-client:google-api-client-android:1.22.0'
    compile 'com.google.http-client:google-http-client-gson:1.22.0'
}
repositories {
    mavenCentral()
}
