apply plugin: 'com.android.application'

def gitTag = 'git tag'.execute([], project.rootDir).text.trim().split("\n").last()
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

def keystorePSW = ''
def keystoreAlias = ''
def keystoreAliasPSW = ''
// default keystore file, PLZ config file path in local.properties
def keyFile = file('s.keystore.temp')

Properties properties = new Properties()
// local.properties file in the root director
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def keystoreFilepath = properties.getProperty("keystore.path")

if (keystoreFilepath) {
    keystorePSW = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPSW = properties.getProperty("keystore.alias_password")
    keyFile = file(keystoreFilepath)
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    signingConfigs {
        myConfig {
            storeFile keyFile
            storePassword keystorePSW
            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
        }
    }
    defaultConfig {
        applicationId "gedoor.kunfei.lunarreminder"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 10000 + gitCommits
        versionName gitTag
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        project.ext.set("archivesBaseName", "LunarReminder-" + gitTag)
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.myConfig
        }
        release {
            signingConfig signingConfigs.myConfig
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }
    flavorDimensions "default"
    productFlavors {
        github {
            dimension "default"
            manifestPlaceholders = [UMENG_CHANNEL: "GitHub"]
        }
        google {
            dimension "default"
            manifestPlaceholders = [UMENG_CHANNEL: "Google"]
            buildConfigField "boolean", "FIR_OTA_ENABLED", "false"
        }
        coolapk {
            dimension "default"
            manifestPlaceholders = [UMENG_CHANNEL: "CookApk"]
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
        exclude group: "org.apache.httpcomponents", module: "httpclient"
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'
    compile 'pub.devrel:easypermissions:0.3.0'
    compile 'cn.carbs.android:IndicatorView:1.0.2'
    compile 'cn.carbswang.android:NumberPickerView:1.1.0'
    compile 'com.google.apis:google-api-services-calendar:v3-rev239-1.22.0'
    compile 'com.google.android.gms:play-services-auth:11.4.2'
    compile 'com.google.api-client:google-api-client-android:1.22.0'
    compile 'com.google.http-client:google-http-client-gson:1.22.0'
    compile 'com.umeng.analytics:analytics:6.1.2'

}
repositories {
    mavenCentral()
}
